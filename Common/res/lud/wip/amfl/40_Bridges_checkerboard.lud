(define "Nbor"
    (count Sites
        in:(intersection
            (sites Around (#1))
            (sites Occupied by:#2)
        )
    )
)

(define "CheckCaptures"
    (and
        // Suicide
        (if (<= 3 ("Nbor" #1 Enemy))
            (remove #1)
        )
        // Captures
        (forEach Site (sites Occupied by:Enemy)
            (if (<= 3 ("Nbor" site Friend))
                (remove (site))
            )
        )
    )
)

(game "40 Bridges"
    (players 2)
    (equipment {
        (board (square Diamond 5))
        (piece "Disc" Each
            (or
                (move Step
                    (directions Diagonal)
                    (to if:(is Empty (to)))
                    (then ("CheckCaptures" (last To)))
                )
                (move Hop
                    (between if:(is Friend (who at:(between))))
                    (to if:(is Empty (to)))
                    (then ("CheckCaptures" (last To)))
                )
            )
        )
    })
    (rules
        (start
            {
                (place "Disc1" {"E1" "D2" "E2" "F2" "D3" "E3" "F3" "E4"})
                (place "Disc2" {"E9" "D8" "E8" "F8" "D7" "E7" "F7" "E6"})
             })
        (play
            (forEach Piece)
        )
        (end {
            (if (is Within (id "Disc" P1) in:(sites "E9") ) (result P1 Win))
            (if (is Within (id "Disc" P2) in:(sites "E1") ) (result P2 Win))
        })
    )
)

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "40 Bridges is a game played on a small board inspired by a tiled floor.")
        (rules "The object is to be the first player to move one of their pieces to the opposing corner square or to capture all their opponent's pieces. Pieces move diagonally into empty spaces, or by jumping over one adjacent piece of their own color. Note that this results in a piece being unable to leave the square color it begins the game on. A player can capture an opponent's piece by surrounding it with 3 pieces that are adjacent to it.")
        (source "https://www.youtube.com/watch?v=xlFHDcK4NOs Dec 5 2020")
        (version "1.2.5")
        (classification "experimental")
        (credit "Pocket83")
        }
    )
    (graphics
        {
            (board Style Chess)
        }
    )
)
